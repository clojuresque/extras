buildscript {
    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.1.0"
    }
}

subprojects {
    group = 'clojuresque'
    version = '1.1.0'

    ext.kotka = [
        repository:     "clojuresque/extras",
        wrapperVersion: "1.10"
    ]

    apply plugin: 'kotka'

    repositories {
        mavenCentral()
    }
}

project(':clojuresque-extras') {
    description = "A Clojure plugin for gradle (extra tasks)"

    apply plugin: 'groovy'

    dependencies {
        compile gradleApi()
        compile localGroovy()

        testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
    }

    compileGroovy {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    processResources {
        inputs.property "version", {-> project.version}
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }

    task groovydocJar(type: Jar) {
        classifier = "javadoc"
        from project.groovydoc.outputs
    }

    task sourcesJar(type: Jar) {
        classifier = "sources"
        from project.sourceSets.main.allSource
        exclude "**/.*.swp"
    }

    artifacts {
        archives project.groovydocJar
        archives project.sourcesJar
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}
